<!DOCTYPE xsl:stylesheet [

    <!ENTITY nbsp       "&#160;">

]>
<!--

    Stylesheet with templates to format figures, to be imported in
    tei2html.xsl.

    Requires:
        localization.xsl    : templates for localizing strings.

-->

<xsl:stylesheet
    xmlns="http://www.w3.org/1999/xhtml"
    xmlns:f="urn:stylesheet-functions"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:img="http://www.gutenberg.ph/2006/schemas/imageinfo"
    xmlns:xd="http://www.pnp-software.com/XSLTdoc"
    version="2.0"
    exclude-result-prefixes="f img xd xs"
    >

    <xd:doc type="stylesheet">
        <xd:short>TEI stylesheet to handle figures.</xd:short>
        <xd:detail>This stylesheet handles TEI figure elements.</xd:detail>
        <xd:author>Jeroen Hellingman</xd:author>
        <xd:copyright>2014, Jeroen Hellingman</xd:copyright>
    </xd:doc>


    <xd:doc type="string">
        The imageInfoFile is an XML file that contains information on the dimensions of images.
        This file is generated by an external tool.
    </xd:doc>

    <xsl:param name="imageInfoFile"/>


    <xd:doc>
        <xd:short>Determine the file name for an image.</xd:short>
        <xd:detail>
            <p>Derive a file name from the unique id, and assume that the format
            is .jpg, unless an alternative name is given in the rend attribute, using
            image().</p>
        </xd:detail>
        <xd:param name="format" type="string">The default file-extension of the image file.</xd:param>
    </xd:doc>

    <xsl:template name="getimagefilename">
        <xsl:param name="format" select="'.jpg'" as="xs:string"/>
        <xsl:choose>
            <xsl:when test="contains(@rend, 'image(')">
                <xsl:value-of select="substring-before(substring-after(@rend, 'image('), ')')"/>
            </xsl:when>
            <xsl:when test="@url">
                <xsl:value-of select="@url"/>
                <xsl:message terminate="no">WARNING: using non-standard attribute url on figure.</xsl:message>
            </xsl:when>
            <xsl:otherwise>
                <xsl:text>images/</xsl:text><xsl:value-of select="@id"/><xsl:value-of select="$format"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>


    <xd:doc>
        <xd:short>Determine the file name for an image.</xd:short>
        <xd:detail>
            <p>Derive a file name from the unique id, and assume that the format
            is .jpg, unless an alternative name is given in the rend attribute, using
            image().</p>

            <p>This function should replace the similarly named named template.</p>
        </xd:detail>
        <xd:param name="node" type="node()">The figure element for which the file name needs to be determined.</xd:param>
        <xd:param name="defaultformat" type="string">The default file-extension of the image file.</xd:param>
    </xd:doc>

    <xsl:function name="f:getimagefilename" as="xs:string">
        <xsl:param name="node" as="node()"/>
        <xsl:param name="defaultformat" as="xs:string"/>
        <xsl:choose>
            <xsl:when test="contains($node/@rend, 'image(')">
                <xsl:value-of select="substring-before(substring-after($node/@rend, 'image('), ')')"/>
            </xsl:when>
            <xsl:when test="$node/@url">
                <xsl:value-of select="$node/@url"/>
                <xsl:message terminate="no">WARNING: using non-standard attribute url on figure.</xsl:message>
            </xsl:when>
            <xsl:otherwise>
                <xsl:text>images/</xsl:text><xsl:value-of select="$node/@id"/><xsl:value-of select="$defaultformat"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>


    <xd:doc>
        <xd:short>Insert an image in the output (step 1).</xd:short>
        <xd:detail>
            <p>Insert all the required output for an inline image in HTML.</p>

            <p>This template generates the elements surrounding the actual image tag in the output.</p>
        </xd:detail>
        <xd:param name="alt" type="string">The text to be placed on the HTML alt attribute.</xd:param>
        <xd:param name="format" type="string">The default file-extension of the image file.</xd:param>
    </xd:doc>

    <xsl:template name="insertimage">
        <xsl:param name="alt" select="''" as="xs:string"/>
        <xsl:param name="format" select="'.jpg'" as="xs:string"/>

        <!-- Should we link to an external image? -->
        <xsl:choose>
            <xsl:when test="contains(@rend, 'link(')">
                <xsl:variable name="url" select="substring-before(substring-after(@rend, 'link('), ')')"/>
                <a>
                    <xsl:choose>
                        <xsl:when test="$outputformat = 'epub' and matches($url, '^[^:]+\.(jpg|png|gif|svg)$')">
                            <!-- cannot directly link to image file in epub, need to generate wrapper html
                                 and link to that. -->
                            <xsl:call-template name="generate-image-wrapper">
                                <xsl:with-param name="imagefile" select="$url"/>
                            </xsl:call-template>
                            <xsl:attribute name="href"><xsl:value-of select="$basename"/>-<xsl:call-template name="generate-id"/>.xhtml</xsl:attribute>
                        </xsl:when>
                        <xsl:otherwise>
                            <xsl:attribute name="href">
                                <xsl:value-of select="$url"/>
                            </xsl:attribute>
                        </xsl:otherwise>
                    </xsl:choose>
                    <xsl:call-template name="insertimage2">
                        <xsl:with-param name="alt" select="$alt"/>
                        <xsl:with-param name="format" select="$format"/>
                    </xsl:call-template>
                </a>
            </xsl:when>
            <xsl:otherwise>
                <xsl:call-template name="insertimage2">
                    <xsl:with-param name="alt" select="$alt"/>
                    <xsl:with-param name="format" select="$format"/>
                </xsl:call-template>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>


    <xd:doc>
        <xd:short>Insert an image in the output (step 2).</xd:short>
        <xd:detail>
            <p>Insert the actual img-element in the output HTML.</p>
        </xd:detail>
        <xd:param name="alt" type="string">The text to be placed on the HTML alt attribute.</xd:param>
        <xd:param name="format" type="string">The default file-extension of the image file.</xd:param>
        <xd:param name="filename" type="string">The name of the image file (may be left empty).</xd:param>
    </xd:doc>

    <xsl:template name="insertimage2">
        <xsl:param name="alt" select="''" as="xs:string"/>
        <xsl:param name="format" select="'.jpg'" as="xs:string"/>
        <xsl:param name="filename" select="''" as="xs:string"/>

        <!-- What is the text that should go on the img alt attribute in HTML? -->
        <xsl:variable name="alt2">
            <xsl:choose>
                <xsl:when test="figDesc">
                    <xsl:value-of select="figDesc"/>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="$alt"/>
                </xsl:otherwise>
            </xsl:choose>
        </xsl:variable>

        <xsl:variable name="file">
            <!-- Did we get the file name as parameter? -->
            <xsl:choose>
                <xsl:when test="$filename = ''">
                    <xsl:call-template name="getimagefilename">
                        <xsl:with-param name="format" select="$format"/>
                    </xsl:call-template>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="$filename"/>
                </xsl:otherwise>
            </xsl:choose>
        </xsl:variable>

        <xsl:variable name="width">
            <xsl:value-of select="substring-before(document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@width, 'px')"/>
        </xsl:variable>
        <xsl:variable name="height">
            <xsl:value-of select="substring-before(document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@height, 'px')"/>
        </xsl:variable>

        <img>
            <xsl:attribute name="src">
                <xsl:value-of select="$file"/>
            </xsl:attribute>
            <xsl:attribute name="alt">
                <xsl:value-of select="$alt2"/>
            </xsl:attribute>
            <xsl:if test="$width != ''">
                <xsl:attribute name="width">
                    <xsl:value-of select="$width"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:if test="$height != ''">
                <xsl:attribute name="height">
                    <xsl:value-of select="$height"/>
                </xsl:attribute>
            </xsl:if>
        </img>
    </xsl:template>


    <xd:doc>
        <xd:short>Generate an image wrapper for ePub.</xd:short>
        <xd:detail>
            <p>Since images may not appear stand-alone in an ePub file, this generates
            an HTML wrapper for (mostly large) images linked to from a smaller image using
            link() in the rend attribute.</p>
        </xd:detail>
        <xd:param name="imagefile" type="string">The name of the image file (may be left empty).</xd:param>
    </xd:doc>

    <xsl:template name="generate-image-wrapper">
        <xsl:param name="imagefile" as="xs:string"/>

        <xsl:variable name="filename"><xsl:value-of select="$basename"/>-<xsl:call-template name="generate-id"/>.xhtml</xsl:variable>

        <xsl:result-document href="{$path}/{$filename}">
            <xsl:message terminate="no">INFO:    generated file: <xsl:value-of select="$path"/>/<xsl:value-of select="$filename"/>.</xsl:message>
            <html>
                <xsl:call-template name="generate-html-header"/>
                <body>
                    <div class="figure">

                        <img src="{$imagefile}">
                            <xsl:attribute name="alt">
                                <xsl:choose>
                                    <xsl:when test="figDesc">
                                        <xsl:value-of select="figDesc"/>
                                    </xsl:when>
                                    <xsl:when test="head">
                                        <xsl:value-of select="head"/>
                                    </xsl:when>
                                    <xsl:otherwise>
                                        <xsl:value-of select="''"/>
                                    </xsl:otherwise>
                                </xsl:choose>
                            </xsl:attribute>
                        </img>

                        <xsl:apply-templates/>
                    </div>
                </body>
            </html>
        </xsl:result-document>
    </xsl:template>


    <!-- TEI P5 graphic element -->
    <xsl:template match="graphic">
        <xsl:if test="$optionIncludeImages = 'Yes'">
            <xsl:call-template name="insertimage">
                <xsl:with-param name="format" select="'.png'"/>
            </xsl:call-template>
        </xsl:if>
    </xsl:template>


    <xd:doc>
        <xd:short>Handle an in-line image.</xd:short>
        <xd:detail>
            <p>Special handling of figures marked as inline using the rend attribute.</p>
        </xd:detail>
    </xd:doc>

    <xsl:template match="figure[@rend='inline' or contains(@rend, 'position(inline)')]">
        <xsl:if test="$optionIncludeImages = 'Yes'">
            <xsl:call-template name="insertimage">
                <xsl:with-param name="format" select="'.png'"/>
            </xsl:call-template>
        </xsl:if>
    </xsl:template>


    <xd:doc>
        <xd:short>Generate CSS code related to images.</xd:short>
        <xd:detail>
            <p>In the CSS for each image, we register its length and width, to help
            HTML rendering.</p>
        </xd:detail>
    </xd:doc>

    <xsl:template match="figure" mode="css">
        <xsl:if test="$optionIncludeImages = 'Yes'">
            <!-- Generate CCS class the normal way -->
            <xsl:call-template name="generate-css-rule"/>

            <!-- Create a special CSS rule for setting the width of this image -->
            <xsl:variable name="file">
                <xsl:call-template name="getimagefilename"/>
            </xsl:variable>
            <xsl:variable name="width">
                <xsl:value-of select="document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@width"/>
            </xsl:variable>

            <xsl:if test="$width != ''">
.x<xsl:value-of select="generate-id()"/>width
{
    width:<xsl:value-of select="$width"/>;
}
            </xsl:if>

            <xsl:apply-templates mode="css"/>
        </xsl:if>
    </xsl:template>


    <xd:doc>
        <xd:short>Handle a figure element.</xd:short>
        <xd:detail>
            <p>This template handles the figure element. It takes care of both the figure annotations (title, legenda, etc.)
            and the in-line loading of the image in HTML.</p>
        </xd:detail>
    </xd:doc>

    <xsl:template match="figure">
        <xsl:if test="$optionIncludeImages = 'Yes'">
            <xsl:call-template name="closepar"/>
            <div class="figure">
                <xsl:call-template name="set-lang-id-attributes"/>

                <xsl:variable name="file">
                    <xsl:call-template name="getimagefilename"/>
                </xsl:variable>
                <xsl:variable name="width">
                    <xsl:value-of select="document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@width"/>
                </xsl:variable>

                <xsl:if test="$width = ''">
                    <xsl:message terminate="no">WARNING: Image "<xsl:value-of select="$file"/>" not present in imageinfo file "<xsl:value-of select="normalize-space($imageInfoFile)"/>".</xsl:message>
                </xsl:if>

                <xsl:attribute name="class">
                    <xsl:text>figure </xsl:text>
                    <xsl:if test="contains(@rend, 'float(left)')">floatLeft </xsl:if>
                    <xsl:if test="contains(@rend, 'float(right)')">floatRight </xsl:if>
                    <xsl:call-template name="generate-rend-class-name-if-needed"/><xsl:text> </xsl:text>
                    <!-- Add the class that sets the width, if the width is known -->
                    <xsl:if test="$width != ''">x<xsl:value-of select="generate-id()"/><xsl:text>width</xsl:text></xsl:if>
                </xsl:attribute>

                <xsl:call-template name="figure-annotations-top"/>

                <xsl:call-template name="insertimage">
                    <xsl:with-param name="alt" select="if (figDesc) then figDesc else (if (head) then head else '')"/>
                </xsl:call-template>

                <xsl:call-template name="figure-annotations-bottom"/>

                <xsl:apply-templates/>
            </div>
            <xsl:call-template name="reopenpar"/>
        </xsl:if>
    </xsl:template>


    <xsl:template name="figure-annotations-top">
        <xsl:if test="p[f:hasTopPositionAnnotation(@rend)]">

            <xsl:variable name="file">
                <xsl:call-template name="getimagefilename"/>
            </xsl:variable>
            <xsl:variable name="width">
                <xsl:value-of select="document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@width"/>
            </xsl:variable>

            <div>
                <xsl:attribute name="class">
                    <xsl:text>figAnnotation </xsl:text>
                    <xsl:if test="$width != ''">x<xsl:value-of select="generate-id()"/><xsl:text>width</xsl:text></xsl:if>
                </xsl:attribute>

                <xsl:if test="p[f:positionAnnotation(@rend) = 'figTopLeft']">
                    <span class="figTopLeft"><xsl:apply-templates select="p[@type='figTopLeft' or contains(@rend, 'position(figTopLeft)')]" mode="figAnnotation"/></span>
                </xsl:if>
                <xsl:if test="p[f:positionAnnotation(@rend) = 'figTop']">
                    <span class="figTop"><xsl:apply-templates select="p[@type='figTop' or contains(@rend, 'position(figTop)')]" mode="figAnnotation"/></span>
                </xsl:if>
                <xsl:if test="not(p[f:positionAnnotation(@rend) = 'figTop'])">
                    <span class="figTop"><xsl:text>&nbsp;</xsl:text></span>
                </xsl:if>
                <xsl:if test="p[f:positionAnnotation(@rend) = 'figTopRight']">
                    <span class="figTopRight"><xsl:apply-templates select="p[@type='figTopRight' or contains(@rend, 'position(figTopRight)')]" mode="figAnnotation"/></span>
                </xsl:if>
            </div>
        </xsl:if>
    </xsl:template>


    <xsl:template name="figure-annotations-bottom">
        <xsl:if test="p[f:hasBottomPositionAnnotation(@rend)]">

            <xsl:variable name="file">
                <xsl:call-template name="getimagefilename"/>
            </xsl:variable>
            <xsl:variable name="width">
                <xsl:value-of select="document(normalize-space($imageInfoFile), .)/img:images/img:image[@path=$file]/@width"/>
            </xsl:variable>

            <div>
                <xsl:attribute name="class">
                    <xsl:text>figAnnotation </xsl:text>
                    <xsl:if test="$width != ''">x<xsl:value-of select="generate-id()"/><xsl:text>width</xsl:text></xsl:if>
                </xsl:attribute>

                <xsl:if test="p[f:positionAnnotation(@rend) = 'figBottomLeft']">
                    <span class="figBottomLeft"><xsl:apply-templates select="p[@type='figBottomLeft' or contains(@rend, 'position(figBottomLeft)')]" mode="figAnnotation"/></span>
                </xsl:if>
                <xsl:if test="p[f:positionAnnotation(@rend) = 'figBottom']">
                    <span class="figBottom"><xsl:apply-templates select="p[@type='figBottom' or contains(@rend, 'position(figBottom)')]" mode="figAnnotation"/></span>
                </xsl:if>
                <xsl:if test="not(p[f:positionAnnotation(@rend) = 'figBottom'])">
                    <span class="figTop"><xsl:text>&nbsp;</xsl:text></span>
                </xsl:if>
                <xsl:if test="p[f:positionAnnotation(@rend) = 'figBottomRight']">
                    <span class="figBottomRight"><xsl:apply-templates select="p[@type='figBottomRight' or contains(@rend, 'position(figBottomRight)')]" mode="figAnnotation"/></span>
                </xsl:if>
            </div>
        </xsl:if>
    </xsl:template>


    <xsl:function name="f:hasPositionAnnotation" as="xs:boolean">
        <xsl:param name="rend"/>

        <xsl:value-of select="f:positionAnnotation($rend) != ''"/>
    </xsl:function>

    <xsl:function name="f:hasTopPositionAnnotation" as="xs:boolean">
        <xsl:param name="rend"/>

        <xsl:value-of select="f:topPositionAnnotation($rend) != ''"/>
    </xsl:function>

    <xsl:function name="f:hasBottomPositionAnnotation" as="xs:boolean">
        <xsl:param name="rend"/>

        <xsl:value-of select="f:bottomPositionAnnotation($rend) != ''"/>
    </xsl:function>

    <xsl:function name="f:positionAnnotation" as="xs:string">
        <xsl:param name="rend"/>

        <xsl:variable name="position" select="substring-before(substring-after($rend, 'position('), ')')"/>

        <xsl:value-of select="if ($position = 'figTopLeft' or $position = 'figTop' or $position = 'figTopRight'
                                or $position = 'figBottomLeft' or $position = 'figBottom' or $position = 'figBottomRight') then $position else ''"/>
    </xsl:function>

    <xsl:function name="f:topPositionAnnotation" as="xs:string">
        <xsl:param name="rend"/>

        <xsl:variable name="position" select="substring-before(substring-after($rend, 'position('), ')')"/>

        <xsl:value-of select="if ($position = 'figTopLeft' or $position = 'figTop' or $position = 'figTopRight') then $position else ''"/>
    </xsl:function>

     <xsl:function name="f:bottomPositionAnnotation" as="xs:string">
        <xsl:param name="rend"/>

        <xsl:variable name="position" select="substring-before(substring-after($rend, 'position('), ')')"/>

        <xsl:value-of select="if ($position = 'figBottomLeft' or $position = 'figBottom' or $position = 'figBottomRight') then $position else ''"/>
    </xsl:function>

    <xsl:template match="figure/head">
        <p class="figureHead"><xsl:apply-templates/></p>
    </xsl:template>


    <xsl:template match="p[f:hasPositionAnnotation(@rend)]"/>


    <xsl:template match="p[f:hasPositionAnnotation(@rend)]" mode="figAnnotation">
        <xsl:apply-templates/>
    </xsl:template>


    <xsl:template match="figDesc"/>


</xsl:stylesheet>
